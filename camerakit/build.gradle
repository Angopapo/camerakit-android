plugins {
    id 'me.tatarka.retrolambda' version '3.7.0'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.dcendents.android-maven' version '1.5'
}

apply plugin: 'com.android.library'

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            java {
                setSrcDirs([])
                srcDir 'src/main/java/public'
                srcDir 'src/main/java/public.types'
                srcDir 'src/main/java/public.products'
                srcDir 'src/main/java/core'
                srcDir 'src/main/java/core.camera'
                srcDir 'src/main/java/core.camera.flash'
                srcDir 'src/main/java/core.camera.focus'
                srcDir 'src/main/java/core.camera.photo'
                srcDir 'src/main/java/core.camera.video'
                srcDir 'src/main/java/core.camera.zoom'
                srcDir 'src/main/java/core.preview'
            }
        }
    }
}

dependencies {
    implementation 'com.camerakit:jpegkit:0.0.1'
    testImplementation 'junit:junit:4.12'
}

ext.getBintrayUser = { ->
    if (System.getenv('BINTRAY_USER')) {
        return System.getenv('BINTRAY_USER')
    }

    Properties properties = new Properties()
    properties.load(rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty('bintray.user')
}

ext.getBintrayKey = { ->
    if (System.getenv('BINTRAY_KEY')) {
        return System.getenv('BINTRAY_KEY')
    }

    Properties properties = new Properties()
    properties.load(rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty('bintray.key')
}

task cleanDeployment {
    doLast {
        logger.lifecycle('CameraKit Deployment: Cleaning...')
        logger.lifecycle('Deleting: ' + project.buildDir)
        delete project.buildDir
    }
}

task buildDeployment {
    shouldRunAfter(cleanDeployment)
    finalizedBy assemble

    doFirst {
        android.variantFilter { variant ->
            if (variant.buildType.name == 'debug') {
                variant.setIgnore(true)
            }
        }
    }
}

task deploySnapshot {
    shouldRunAfter(buildDeployment)

    dependsOn cleanDeployment
    dependsOn buildDeployment
    finalizedBy bintrayUpload

    doLast {
        version = version + '-Snapshot'
        bintrayUpload.setVersionName(bintrayUpload.getVersionName() + '-Snapshot')
        bintrayUpload.setUserOrg('camerakit')
        bintrayUpload.setRepoName('camerakit-android-snapshots')
        bintrayUpload.setOverride(true)
        bintrayUpload.setPublish(true)

        logger.lifecycle('Deploying version ' + bintrayUpload.getVersionName() + ' in ' + bintrayUpload.getRepoName())
    }
}

task deployRelease {
    shouldRunAfter(buildDeployment)

    dependsOn cleanDeployment
    dependsOn buildDeployment
    finalizedBy bintrayUpload

    doLast {
        bintrayUpload.setUserOrg('camerakit')
        bintrayUpload.setRepoName('camerakit-android')

        logger.lifecycle('Deploying version ' + bintrayUpload.getVersionName() + ' in ' + bintrayUpload.getRepoName())
    }
}

group = 'com.camerakit'
version = '1.0.0'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'camerakit-android'
            groupId 'com.camerakit'
            artifactId 'camerakit'
            packaging 'aar'
        }
    }
}

bintray {
    user = getBintrayUser()
    key = getBintrayKey()
    configurations = ['archives']
    pkg {
        name = 'camerakit-android'
        vcsUrl = 'https://github.com/CameraKit/camerakit-android.git'
        licenses = ['MIT']
        version {
            name = project.version
            released = new Date()
        }
    }
}
